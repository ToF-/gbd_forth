REQUIRE set.fs

VARIABLE #DRIVERS
65536 CONSTANT MAXSTOP
64    CONSTANT MAXDRIVER
480   CONSTANT MAXTIME
MAXTIME MAXDRIVER * CONSTANT MAXROUTE

CREATE STOPS   MAXSTOP   CELLS ALLOT
CREATE DRIVERS MAXDRIVER CELLS ALLOT
CREATE ROUTES  MAXROUTE  CELLS ALLOT
CREATE LENGTH  MAXDRIVER CELLS ALLOT

: DRIVER-MINUTE-ROUTE! ( stop,driver,minute -- )
  SWAP MAXTIME * + CELLS ROUTES + ! ;

: MINUTE-DRIVER-STOP ( minute,driver -- stop )
  LENGTH OVER CELLS + @ 
  ROT SWAP MOD
  SWAP MAXTIME * + CELLS ROUTES + @ ;

: CLEAR-STOPS
  STOPS MAXSTOP CELLS ERASE ;

: STOP ( driver -- addr )
  CELLS STOPS + ;

: DRIVER-STOP! ( driver,stop -- )
  STOP DUP @
  ROT SWAP IN-SET!
  SWAP ! ;

: DRIVER ( driver -- addr )
  CELLS DRIVERS + ;

: INIT-DRIVERS
  DRIVERS MAXDRIVER CELLS ERASE
  #DRIVERS @ 0 DO
    I EMPTY-SET IN-SET!
    I DRIVER !
  LOOP ;

: DRIVERS-AT-STOP ( stop -- set )
  STOP @ ;

: GOSSIP-AT-STOP ( stop -- set )
  0 SWAP DRIVERS-AT-STOP
  #DRIVERS @ 0 DO
    DUP I IN-SET? IF
      I DRIVER @
      SWAP UNION-SET SWAP
    THEN
  LOOP DROP ;

: GOSSIP! ( stop -- )
  DUP GOSSIP-AT-STOP
  SWAP DRIVERS-AT-STOP
  #DRIVERS @ 0 DO
    DUP I IN-SET? IF
      OVER I DRIVER !
    THEN
  LOOP DROP DROP ;

: COMPLETE ( -- f )
  #DRIVERS @ FULL-SET
  #DRIVERS @ 0 DO
    I DRIVER @ AND
  LOOP 
  #DRIVERS @ FULL-SET = ;
